plugins {
    id 'java-library'
    id 'io.qameta.allure' version '2.11.2' // Обновили версию для стабильности в 2026 году
    id "io.freefair.lombok" version "8.4" // Обновленная версия lombok
}

group = 'kozherka.ru'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

def restAssuredVersion = "5.3.1"
def allureVersion = "2.25.0"
def selenideVersion = "6.16.1"
def appiumVersion = "8.3.0"


tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

dependencies {
    // REST API
    testImplementation "io.rest-assured:rest-assured:$restAssuredVersion"

    // UI (Selenide)
    testImplementation "com.codeborne:selenide:$selenideVersion"

    // MOBILE
    testImplementation "io.appium:java-client:$appiumVersion"
    testImplementation "org.aeonbits.owner:owner:1.0.12"

    // ALLURE
    testImplementation "io.qameta.allure:allure-rest-assured:$allureVersion"
    testImplementation "io.qameta.allure:allure-selenide:$allureVersion"
    testImplementation "io.qameta.allure:allure-junit5:$allureVersion"

    // INFRASTRUCTURE
    testImplementation "org.junit.jupiter:junit-jupiter:5.10.0"
    testImplementation 'org.slf4j:slf4j-simple:2.0.7'
    testImplementation 'org.freemarker:freemarker:2.3.31'
}

allure {
    report {
        version.set(allureVersion)
    }
    adapter {
        aspectjWeaver.set(true)
        frameworks {
            junit5 {
                adapterVersion.set(allureVersion)
            }
        }
    }
}

tasks.withType(Test) {

    systemProperty "file.encoding", "UTF-8"
    systemProperty "sun.stdout.encoding", "UTF-8"
    systemProperty "sun.stderr.encoding", "UTF-8"

    useJUnitPlatform {
        String tags = System.getProperty("junit.platform.include.tags")
        if (tags != null && !tags.isEmpty()) {
            includeTags tags
        }
    }


    systemProperties(System.getProperties())

    testLogging {
        lifecycle {
            events "started", "skipped", "failed", "standard_error", "standard_out"
            exceptionFormat "short"
        }
    }
}
